grammar org.tetrabox.minijava.xtext.MiniJava with org.eclipse.xtext.common.Terminals

generate miniJava "http://www.example.org/minijava/MiniJava"

Program:
	('package' name=QualifiedName ';')?
	imports+=Import*
	classes+=Class*;

Import:
	'import' importedNamespace=QualifiedNameWithWildcard ';';

QualifiedName:
	ID ('.' ID)*;

QualifiedNameWithWildcard:
	QualifiedName '.*'?;

Class:
	accessLevel=AccessLevel? 'class' name=ID ('extends' superclass=[Class|QualifiedName])? '{'
	members+=Member*
	'}';

Member:
	Field | Method;

Parameter:
	typeRef=TypeRef name=ID;

Field:
	access=AccessLevel? typeRef=TypeRef name=ID ';';

Method:
	access=AccessLevel? static?='static'? typeRef=TypeRef name=ID
	'(' (params+=Parameter (',' params+=Parameter)*)? ')'
	body=Block;

enum AccessLevel:
	PRIVATE='private' | PROTECTED='protected' | PUBLIC='public';

Block:
	{Block} '{' statements+=Statement* '}';

Statement:
	VariableDeclaration ';' |
	Return ';' |
	=> Assignment ';' |
	IfStatement |
	WhileStatement |
	ForStatement |
	Block | 
	SelectionExpression ';';

VariableDeclaration:
	typeRef=TypeRef name=ID '=' expression=Expression;

Return:
	'return' expression=Expression;

IfStatement:
	'if' '(' expression=Expression ')' thenBlock=Block
	(=> 'else' elseBlock=Block)?;

//IfBlock returns Block:
//	statements+=Statement
//	| Block;

WhileStatement:
	'while' '(' condition=Expression ')' block=Block;
	
ForStatement:
	'for' '(' declaration=VariableDeclaration ';' condition=Expression ';' progression=Assignment ')' block=Block
;

TypeRef:
	SingleTypeRef ({ArrayTypeRef.typeRef=current}'[' ']')?
;

SingleTypeRef:
	ClassRef |
	{IntegerTypeRef} "int" |
	{BooleanTypeRef} "boolean" |
	{StringTypeRef} "String" |
	{VoidTypeRef} "void";
	


ClassRef:
	class=[Class|QualifiedName];

NamedElement:
	TypedDeclaration | Class;

TypedDeclaration:
	Symbol | Member;

Symbol:
	Parameter | VariableDeclaration;

Assignment:
	left=SelectionExpression '=' right=Expression;

Expression:
	Or;

Or returns Expression:
	And ({Or.left=current} "||" right=And)*;

And returns Expression:
	Equality ({And.left=current} "&&" right=Equality)*;

Equality returns Expression:
	Comparison ({Equality.left=current} op=("==" | "!=")
	right=Comparison)*;

Comparison returns Expression:
	PlusOrMinus ({Comparison.left=current} op=(">=" | "<=" | ">" | "<")
	right=PlusOrMinus)*;

PlusOrMinus returns Expression:
	MulOrDiv (({Plus.left=current} '+' | {Minus.left=current} '-')
	right=MulOrDiv)*;

MulOrDiv returns Expression:
	Primary (({MulOrDiv.left=current} op=('*' | '/'))
	right=Primary)*;

Primary returns Expression:
	'(' Expression ')' |
	{Not} => "!" expression=Primary |
	SelectionExpression;

SelectionExpression returns Expression:
	TerminalExpression
	({MemberSelection.receiver=current} '.'
	member=[Member]
	(methodinvocation?='('
	(args+=Expression (',' args+=Expression)*)? ')')?)*;

TerminalExpression returns Expression:
	{StringConstant} value=STRING |
	{IntConstant} value=INT |
	{BoolConstant} value=('true' | 'false') |
	{This} 'this' |
	{Super} 'super' |
	{Null} 'null' |
	{New} 'new' type=[Class|QualifiedName] '(' ')' | //TODO possibility for non default constructors
	{SymbolRef} symbol=[Symbol|ID];
	